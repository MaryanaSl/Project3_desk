  #Область ПрограммныйИнтерфейс
  
  Процедура ДополнитьФорму (Форма) Экспорт
	  
	  ИмяФормы = Форма.ИмяФормы;
	  
	  Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда 
		  ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво (Форма);
		  ДобавитьГруппуСкидка (Форма);
		  ДобавитьПолеСогласованнаяСкидкаВГруппаСкидка (Форма); 
		  ДобавитьКнопкуПересчитатьТаблицуВГруппаСкидка (Форма);
	  ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда 
		  ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво (Форма);
	  ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда 
		  ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво (Форма);
	  ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда 
		  ВставитьПолеКонтактноеЛицоНаФормуПередОснование (Форма);      
	  ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда 
		  ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента (Форма);
	  КонецЕсли;
	  
  КонецПроцедуры 
  
  #КонецОбласти  
  
  # Область СлужебныйПрограммныйИнтерфейс
  
  
  Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво (Форма)
	  
	  ПолеВвода = Форма.Элементы.Добавить ("КонтактноеЛицо", Тип ("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	  ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	  ПолеВвода.ПутьКДанным = "Объект.Доработка_КонтактноеЛицо";
	  
  КонецПроцедуры
  
  Процедура ДобавитьГруппуСкидка (Форма)
	  
	  Группа = Форма.Элементы.Добавить ("ГруппаСкидка", Тип ("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево); 
	  Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	  Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	  
  КонецПроцедуры
  
  
  Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаСкидка (Форма)
	  
	  ПолеВвода = Форма.Элементы.Добавить ("СогласованнаяСкидка", Тип ("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	  ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	  ПолеВвода.ПутьКДанным = "Объект.Доработка_СогласованнаяСкидка"; 
	  
	  //установим действие при изменении реквизита согласованная скидка
	  Форма.Элементы.СогласованнаяСкидка.УстановитьДействие ("ПриИзменении", "Доработка_СогласованнаяСкидкаПриИзменении"); 
  КонецПроцедуры
  
  Процедура ДобавитьКнопкуПересчитатьТаблицуВГруппаСкидка (Форма)
	  
	  Команда = Форма.Команды.Добавить ("ПересчитатьТаблицу");
	  Команда.Заголовок = "Пересчитать таблицу";
	  Команда.Действие = "КомандаПересчитатьТаблицу"; 
	  
	  КнопкаФормы = Форма.Элементы.Добавить ("КнопкаПересчитатьТаблицу", Тип ("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	  КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	  КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;   
	  КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	  КнопкаФормы.Картинка = БиблиотекаКартинок.Обновить;
	  
	  
  КонецПроцедуры
  
  Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента (Форма)
	  
	  ПолеВвода = Форма.Элементы.Вставить ("КонтактноеЛицо", Тип ("ПолеФормы"),,Форма.Элементы.СуммаДокумента);
	  ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	  ПолеВвода.ПутьКДанным = "Объект.Доработка_КонтактноеЛицо";
	  
  КонецПроцедуры   
  
  
  Процедура ВставитьПолеКонтактноеЛицоНаФормуПередОснование (Форма)
	  
	  ПолеВвода = Форма.Элементы.Вставить ("КонтактноеЛицо", Тип ("ПолеФормы"),,Форма.Элементы.Основание);
	  ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	  ПолеВвода.ПутьКДанным = "Объект.Доработка_КонтактноеЛицо";
	  
  КонецПроцедуры   

  #КонецОбласти
  
