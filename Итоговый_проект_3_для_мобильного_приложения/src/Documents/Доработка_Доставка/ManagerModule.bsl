
Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры


Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Анкета клиента
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Анкета клиента'");
	КомандаПечати.Порядок = 5;
	
	// Товарная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная'");
	КомандаПечати.Порядок = 10;
	
	// общая печать 2х Анкеты и товарной накладной
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента,ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 20;

	// печать договора на доставку в Word
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "УправлениеПечатью";
	КомандаПечати.Идентификатор = "Документ.Доработка_Доставка.ПФ_DOC_ДоговорНаДоставку";
	КомандаПечати.Представление = НСтр("ru = 'Договор на доставку (docx)'");
	КомандаПечати.Порядок = 30;

КонецПроцедуры


Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаКлиента");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкеты(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета клиента'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Доработка_Доставка.ПФ_MXL_АнкетаКлиента";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТоварнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТоварнойНакладной(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Товарная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Доработка_Доставка.ПФ_MXL_ТоварнаяНакладная";
	КонецЕсли;
	
	
КонецПроцедуры  

Функция ПечатьАнкеты(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АнкетаКлиента";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Доработка_Доставка.ПФ_MXL_АнкетаКлиента");
	
	ОбластьШапка = Макет.ПолучитьОбласть ("Шапка");  
	
	//запросом вытащим дату и номер
	
	Запрос = Новый Запрос;
	Запрос.Текст = " ВЫБРАТЬ
	                |	Доработка_Доставка.Дата КАК ДатаДокумента,
	                |	Доработка_Доставка.Номер КАК НомерДокумента,
	                |	Доработка_Доставка.Ссылка КАК Ссылка
	                |ИЗ
	                |	Документ.Доработка_Доставка КАК Доработка_Доставка
	                |ГДЕ
	                |	Доработка_Доставка.Ссылка В(&МассивОбъектов)";  
	
	Запрос.УстановитьПараметр("МассивОбъектов",МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьШапка.Параметры.ДатаДокумента = Формат (Выборка.ДатаДокумента, "ДФ=dd.MM.yyyy" + " г.");
		ОбластьШапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.НомерДокумента);
		
		//в алгоритме печати сформируйте QR-код:
		ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ПолучитьНавигационнуюСсылку(Выборка.Ссылка), 1, 120);
		
		//проверьте, что QR-код сформирован успешно и выведите его в документ:
		Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
			|Технические подробности см. в журнале регистрации.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Иначе 
			//генерируем картинку
			КартинкаQRКода = Новый Картинка(ДанныеQRКода);
			//размечаем область шапки
			ОбластьШапка.Рисунки.QRкод.Картинка = КартинкаQRКода;
		КонецЕсли;		
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);		
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьТоварнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТоварнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Доработка_Доставка.ПФ_MXL_ТоварнаяНакладная");
	
	ОбластьШапка = Макет.ПолучитьОбласть ("Шапка"); 
	ОбластьГрузоотправитель = Макет.ПолучитьОбласть ("Грузоотправитель");
	ОбластьГрузополучатель = Макет.ПолучитьОбласть ("Грузополучатель");
	
	ОбластьШапкаТЧ = Макет.ПолучитьОбласть ("ШапкаТЧ");  
	ОбластьСтрокаТЧ = Макет.ПолучитьОбласть ("СтрокаТЧ"); 
	ОбластьГрузПринял = Макет.ПолучитьОбласть ("ГрузПринял");
	
	//запросом вытащим дату и номер
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Доработка_Доставка.Дата КАК ДатаДокумента,
	               |	Доработка_Доставка.Номер КАК НомерДокумента,
	               |	Доработка_Доставка.Ссылка КАК Ссылка,
	               |	Доработка_Доставка.Контрагент КАК Контрагент,
	               |	Доработка_Доставка.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество
	               |	) КАК Товары,
	               |	Доработка_Доставка.Организация КАК Организация
	               |ИЗ
	               |	Документ.Доработка_Доставка КАК Доработка_Доставка
	               |ГДЕ
	               |	Доработка_Доставка.Ссылка В(&МассивОбъектов)";  
	
	Запрос.УстановитьПараметр("МассивОбъектов",МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьШапка.Параметры.ДатаДокумента = Формат (Выборка.ДатаДокумента, "ДФ=dd.MM.yyyy") + " г.";
		ОбластьШапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.НомерДокумента);
		//в алгоритме печати сформируйте QR-код:
		ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ПолучитьНавигационнуюСсылку(Выборка.Ссылка), 1, 120);
		
		//проверьте, что QR-код сформирован успешно и выведите его в документ:
		Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
			|Технические подробности см. в журнале регистрации.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Иначе 
			//генерируем картинку
			КартинкаQRКода = Новый Картинка(ДанныеQRКода);
			//размечаем область шапки
			ОбластьШапка.Рисунки.QRкод.Картинка = КартинкаQRКода;
		КонецЕсли;		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("ПредставлениеГрузоотправителя", Выборка.Организация);
		ДанныеПечати.Вставить("ПредставлениеГрузополучателя", Выборка.Контрагент);
		
		ОбластьГрузоотправитель.Параметры.Заполнить (ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьГрузоотправитель);
		
		ОбластьГрузополучатель.Параметры.Заполнить (ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьГрузополучатель);
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТЧ); 
		
		ВыборкаТЧ = Выборка.Товары.Выбрать();
		Пока ВыборкаТЧ.Следующий() Цикл
			ОбластьСтрокаТЧ.Параметры.Заполнить(ВыборкаТЧ);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТЧ);
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьГрузПринял);  
		
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);		
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

